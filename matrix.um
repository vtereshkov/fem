// Matrix algebra

type Matrix* = [][]real

fn assert(cond: bool, msg: str = "") {
	if !cond {
		error("Assertion failed " + msg)
	}
}

fn zeros*(rows, cols: int): Matrix {
	m := make(Matrix, rows)
	for i in m {
		m[i] = make([]real, cols)
	}
	return m
}

fn (m: ^Matrix) rows*(): int {
	return len(m^)
}

fn (m: ^Matrix) cols*(): int {
	return len(m^) > 0 ? len(m[0]) : 0
}

fn (m: ^Matrix) remove*(rows, cols: map[int]bool): Matrix {
	res := zeros(m.rows() - len(rows), m.cols() - len(cols))
	i := 0
	for mi in m {
		if rows[mi] {continue} 
		j := 0
		for mj in m[mi] {
			if cols[mj] {continue}
			res[i][j] = m[mi][mj]
			j++
		}
		i++
	}
	return res
}

fn (m: ^Matrix) add*(n: Matrix): Matrix {
	assert(m.rows() == n.rows() && m.cols() == n.cols())	
	res := zeros(m.rows(), m.cols())
	for i in res {
		for j in res[i] {
			res[i][j] = m[i][j] + n[i][j]
		}
	}
	return res
}

fn (m: ^Matrix) sub*(n: Matrix): Matrix {
	assert(m.rows() == n.rows() && m.cols() == n.cols())	
	res := zeros(m.rows(), m.cols())
	for i in res {
		for j in res[i] {
			res[i][j] = m[i][j] - n[i][j]
		}
	}
	return res
}

fn (m: ^Matrix) mul*(n: Matrix): Matrix {
	assert(m.cols() == n.rows())	
	res := zeros(m.rows(), n.cols())
	for i in res {
		for j in res[i] {
			for k in n {
				res[i][j] += m[i][k] * n[k][j]
			}
		}
	}
	return res
}

fn (m: ^Matrix) mulf*(a: real): Matrix {	
	res := zeros(m.rows(), m.cols())
	for i in res {
		for j in res[i] {
			res[i][j] = m[i][j] * a
		}
	}
	return res
}

fn (m: ^Matrix) transpose*(): Matrix {
	res := zeros(m.cols(), m.rows())
	for i in res {
		for j in res[i] {
			res[i][j] = m[j][i]
		}
	}
	return res
}