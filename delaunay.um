import (
    "th.um"
)

type (
    Edge* = [2]int
    EdgeSet* = map[Edge]bool
    Triangle* = [3]int
    TriangleSet* = map[Triangle]bool
    TriangleNeighbors = map[Triangle][3]Triangle
)

fn det(a: [3][3]real): real {
    return a[0][0] * (a[1][1] * a[2][2] - a[1][2] * a[2][1]) -
           a[0][1] * (a[1][0] * a[2][2] - a[1][2] * a[2][0]) +
           a[0][2] * (a[1][0] * a[2][1] - a[1][1] * a[2][0])
}

fn isInCircumCircle(pt: th.Vf2, tri: [3]th.Vf2): bool {
    da := tri[0].sub(pt)
    db := tri[1].sub(pt)
    dc := tri[2].sub(pt)

    sqr := fn (x: real): real {return x * x}

    d := det({{da.x, da.y, sqr(da.x) + sqr(da.y)},
              {db.x, db.y, sqr(db.x) + sqr(db.y)},
              {dc.x, dc.y, sqr(dc.x) + sqr(dc.y)}})    
    
    return d > 0.0
}

fn isClockwise(tri: [3]th.Vf2): bool {
    a := tri[1].sub(tri[0])
    b := tri[2].sub(tri[1])

    d := a.x * b.y - a.y * b.x
    return d < 0.0
}

fn isConvex(quad: [4]th.Vf2): bool {
    return isClockwise({quad[0], quad[1], quad[2]}) == isClockwise({quad[2], quad[3], quad[0]}) &&
           isClockwise({quad[0], quad[1], quad[3]}) == isClockwise({quad[1], quad[2], quad[3]})
}

fn (edge: ^Edge) reverse(): Edge {
    return {edge[1], edge[0]}
}

fn (edge: ^Edge) intersects(otherEdge: Edge, pts: []th.Vf2): bool {
    if edge[0] == otherEdge[0] || edge[0] == otherEdge[1] || edge[1] == otherEdge[0] || edge[1] == otherEdge[1] {
        return false
    }
    return isConvex({pts[edge[0]], pts[otherEdge[0]], pts[edge[1]], pts[otherEdge[1]]})
}

fn (edges: ^EdgeSet) isEmpty(): bool {
    for _, exists in edges {
        if exists {
            return false
        }
    }
    return true
}

fn (tri: ^Triangle) hasVertex(i: int): bool {
    return i == tri[0] || i == tri[1] || i == tri[2]
}

fn (tri: ^Triangle) hasEdge(edge: Edge): bool {
    return tri.hasVertex(edge[0]) && tri.hasVertex(edge[1]) 
}

fn (tri: ^Triangle) getEdges(): [3]Edge {
    return {{tri[0], tri[1]}, {tri[1], tri[2]}, {tri[2], tri[0]}}
}

fn (tris: ^TriangleSet) condense(): TriangleSet {
    res := TriangleSet{}
    for tri, exists in tris {
        if exists {
            res[tri] = true
        }
    }
    return res
}

fn getBadTris(pt: th.Vf2, pts: []th.Vf2, tris: TriangleSet): TriangleSet {
    badTris := TriangleSet{}
    for tri, exists in tris {
        if exists && isInCircumCircle(pt, {pts[tri[0]], pts[tri[1]], pts[tri[2]]}) {
            badTris[tri] = true
        }
    }
    return badTris
}

fn getBoundary(tris: TriangleSet): []Edge {
    boundaryEdges := []Edge{}

    for tri, exists in tris {
        if !exists {continue}

        edges := tri.getEdges()

        for _, edge in edges {
            isBoundaryEdge := true

            for otherTri, exists in tris {
                if exists && otherTri != tri && otherTri.hasEdge(edge) {
                    isBoundaryEdge = false
                    break
                }
            }

            if isBoundaryEdge {
                boundaryEdges = append(boundaryEdges, edge)
            }                
        }
    }

    return boundaryEdges    
}

fn getNeighbors(tris: TriangleSet): TriangleNeighbors {
    neighbors := TriangleNeighbors{}

    for tri, exists in tris {
        if !exists {continue}

        edges := tri.getEdges()

        for edgeIndex, edge in edges {
            for otherTri, exists in tris {
                if exists && otherTri != tri && otherTri.hasEdge(edge) {
                    neighbors[tri][edgeIndex] = otherTri
                    break
                }
            }           
        }
    }

    return neighbors
}

fn triangulateConvexHull(pts: []th.Vf2): TriangleSet {
    // Bowyer-Watson method for Delaunay triangulation

    // Add a fake super-triangle
    const BIG_VAL = 10000.0

    ptsWithSuper := append(copy(pts), []th.Vf2{{BIG_VAL, BIG_VAL}, {-BIG_VAL, BIG_VAL}, {0.0, -BIG_VAL}})
    superTri := Triangle{len(pts), len(pts) + 1, len(pts) + 2}
    
    tris := TriangleSet{superTri: true}

    // Add original points one by one
    for ptIndex, pt in pts {
        // Find triangles that get bad (i.e., non-Delaunay) after adding the point
        badTris := getBadTris(pt, ptsWithSuper, tris)

        // Find a bounding polygon for all bad triangles
        badPolyEdges := getBoundary(badTris)

        // Remove bad triangles from triangulation
        for badTri, exists in badTris {
            if exists {
                tris[badTri] = false
            }
        }

        // Retriangulate bad triangles' bounding polygon
        for _, badPolyEdge in badPolyEdges {
            // Create a triangle from the bounding polygon edge and the new point
            tri := Triangle{badPolyEdge[0], badPolyEdge[1], ptIndex}

            // Make sure the triangle is counter-clockwise
            if isClockwise({ptsWithSuper[tri[0]], ptsWithSuper[tri[1]], ptsWithSuper[tri[2]]}) {
                tri[0], tri[1] = tri[1], tri[0]
            }
            
            tris[tri] = true
        }              
    }

    // Remove super-triangle parts from triangulation
    for tri, exists in tris {
        if exists && (tri.hasVertex(superTri[0]) || tri.hasVertex(superTri[1]) || tri.hasVertex(superTri[2])) {
            tris[tri] = false
        } 
    }     

    return tris
}

fn isBoundaryValid(pts: []th.Vf2, boundary: EdgeSet): bool {
    var vertexCount: map[int]int

    for edge, exists in boundary {
        if !exists {continue}

        vertexCount[edge[0]]++
        vertexCount[edge[1]]++

        for otherEdge, otherExists in boundary {
            if otherExists && edge.intersects(otherEdge, pts) {
                return false
            }
        }
    }

    for pt, count in vertexCount {
        if count != 2 {
            return false
        }
    }

    return true
}

fn getMissingEdges(tris: TriangleSet, edges: EdgeSet): EdgeSet {
    missingEdges := copy(edges)

    for tri, exists in tris {
        if !exists {continue}
        for _, edge in tri.getEdges() {
            missingEdges[edge] = false
        }
    }

    return missingEdges
}

fn getIntersectingEdges(pts: []th.Vf2, tris: TriangleSet, newEdge: Edge): EdgeSet {
    intersectingEdges := EdgeSet{}

    for tri, exists in tris {
        if !exists {continue}
        for _, edge in tri.getEdges() {
            if !intersectingEdges[edge] && !intersectingEdges[edge.reverse()] && newEdge.intersects(edge, pts) {
                intersectingEdges[edge] = true                
            }
        }
    }

    return intersectingEdges
}

fn getTrianglePair(tris: TriangleSet, commonEdge: Edge): ([2]Triangle, Edge) {
    var pair: [2]Triangle
    var freeVertices: Edge

    for tri, exists in tris {
        if !exists {continue}

        for edgeIndex, edge in tri.getEdges() {
            if edge == commonEdge {
                pair[0] = tri
                freeVertices[0] = tri[(edgeIndex + 2) % 3]
                break
            } else if edge.reverse() == commonEdge {
                pair[1] = tri
                freeVertices[1] = tri[(edgeIndex + 2) % 3]
                break
            }
        }
    }

    return pair, freeVertices   
}

fn addEdge(pts: []th.Vf2, tris: ^TriangleSet, newEdge: Edge) {
    // Detect all edges intersecting with the new edge and replace them with non-intersecting ones
    intersectingEdges := getIntersectingEdges(pts, tris^, newEdge)

    for !intersectingEdges.isEmpty() {
        for diagonal, exists in intersectingEdges {
            if !exists {continue}

            // Find a quad (i.e., triangle pair) whose diagonal is the edge to be replaced, then construct its other diagonal
            trianglePair, otherDiagonal := getTrianglePair(tris^, diagonal)
            if !diagonal.intersects(otherDiagonal, pts) {continue}

            // Construct a new triangle pair separated by the other diagonal
            newTrianglePair := [2]Triangle{
                {otherDiagonal[0], otherDiagonal[1], diagonal[1]},
                {otherDiagonal[0], diagonal[0], otherDiagonal[1]}
            }

            // Replace the old pair with the new one
            tris[trianglePair[0]] = false
            tris[trianglePair[1]] = false

            tris[newTrianglePair[0]] = true
            tris[newTrianglePair[1]] = true

            // Replace the old diagonal with the new one in the set of intersecting edges
            intersectingEdges[diagonal] = false           
            if newEdge.intersects(otherDiagonal, pts) {
                intersectingEdges[otherDiagonal] = true
            }

            break
        }
    }
}

fn removeRecursively(root: Triangle, tris: ^TriangleSet, neighbors: TriangleNeighbors, boundary: EdgeSet) {
    edges := root.getEdges()    
    tris[root] = false

    for edgeIndex, neighbor in neighbors[root] {
        edge := edges[edgeIndex]
        if tris[neighbor] && !boundary[edge] && !boundary[edge.reverse()] {
            removeRecursively(neighbor, tris, neighbors, boundary)
        }
    }    
}

fn trim(tris: ^TriangleSet, neighbors: TriangleNeighbors, boundary: EdgeSet) {
    // Trim the triangulated convex hull against the boundary
    for tri, exists in tris {
        if !exists {continue}

        for _, edge in tri.getEdges() {
            // If a triangle edge coincides with a boundary segment, but in the reversed direction, the triangle is outside the boundary
            if boundary[edge.reverse()] {
                removeRecursively(tri, tris, neighbors, boundary)
                break
            }
        }
    }
}

fn triangulate*(pts: []th.Vf2, boundary: EdgeSet): TriangleSet {
    if !isBoundaryValid(pts, boundary) {
        return TriangleSet{}
    }

    triangles := triangulateConvexHull(pts)

    // Delaunay triangulation does not guarantee that all boundary edges are already present in it -- add them manually 
    missingEdges := getMissingEdges(triangles, boundary)
    for missingEdge, exists in missingEdges {
        if exists {
            addEdge(pts, &triangles, missingEdge)
        }
    }

    neighbors := getNeighbors(triangles)
    trim(&triangles, neighbors, boundary)

    return triangles.condense()
} 

